#NOTICE: You need to point this to the image craff, preferably the one specified in this repositories README : qsp-driver-support.craff
$disk0_image=
run-command-file "%script%/firststeps.simics"
script-branch {
    #wait until login
    board.serconsole.con.bp-wait-for-console-string "simics@cl-qsp" 
    #agent manager is an object that is used to transfer files or run commands inside the simulation
    $matic0 = (start-agent-manager)
    $agent = ($matic0.connect-to-agent)
    $agent.cd /home/simics/
    $agent.wait-for-job
    $agent.upload-dir sort_magic 
    $agent.wait-for-job
    $agent.run "chmod 777 -R sort_magic"
    $agent.cd "sort_magic"
    ($agent.upload (sim->simics_base + "/src/include/simics/" + "magic-instruction.h"))
    $agent.wait-for-job
    ($agent.run make)
    $agent.wait-for-job
    new-instruction-histogram  name = histogram -connect-all
    histogram.enable-instrumentation
    echo "START: Inside the simulated machine, traverse to the directory with the sort_magic: cd sort_magic\nExecute the program with the included dataset to start: ./sort_magic.o dataset.txt" 
}

$enable_instrumentation_string = "Type in the console: histogram.clear to start remove any data and start the profiling."
$disable_instrumentation_string = "Analyze the data by using: histogram.histogram"
$start_profiling_quicksort_string = "Start profiling the QUICKSORT by enabling the instrumentation!"
$type_in_console_string = "Clear all the data from the current measurements by typing: histogram.clear\nTo start executing the sort and profile it type: run "
$start_profiling_bubblesort_string = "Start profiling the BUBBLESORT by enabling the instrumentation!"

script-branch {
    while (1) { 
        bp.magic.wait-for number = 0 
        echo $start_profiling_quicksort_string
        echo $type_in_console_string
        stop
    }
}

script-branch {
    while (1) { 
        bp.magic.wait-for number = 1
        echo $start_profiling_bubblesort_string
        echo $type_in_console_string
        stop
    }
} 


script-branch {
    while (1) {
        bp.magic.wait-for number = 2
        echo $disable_instrumentation_string
        stop
    }
}
